root ::= break? sep? ((comment | command) (break sep? | <eof>))*
word ::= [^ \t\n(){}:?*,=]+
sep ::= [ \t]+
newLine ::= "\n"
break ::= (sep? newLine)+
comment ::= "//" [^\n]*

flag ::= "-" word ("," word)*
escapeSequence ::= "\\" [^]
constraint ::= "<" (escapeSequence | [^<>])* ">"
defaultValue ::= "(" (escapeSequence | [^()])* ")"
arg ::= word constraint? ("..." | "[]")? ":" word ("?" defaultValue?)?
cmdName ::= word ("," word)*
param ::= flag | arg
paramList ::= (sep param)*

tag ::= word (sep? "=" sep? [^{}\n]+) sep?
entry ::= sep? (tag | command | comment) comment? break sep?

command ::= cmdName paramList break? sep? commandBody
commandBody ::= "{" comment? break? entry* "}"