plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'redempt.ordinate'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
    maven {url = 'https://redempt.dev/'}
}

jar.configure {
    actions.clear()
    dependsOn shadowJar
}

shadowJar {
    archiveClassifier.set('')
}

dependencies {
    implementation 'com.github.Redempt:RedLex:1.3.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
    workingDir = 'test'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'res'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.Redempt'
            artifactId = rootProject.name
            version = System.env.BUILD_VERSION ?: '1.0'
            from components.java
        }
    }
}